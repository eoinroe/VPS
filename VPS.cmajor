graph Synth [[ main ]]
{
    input event std::midi::Message midiIn;
    output stream float out;

    // Language feature not yet implemented: top-level arrays of inputs!
    // input voices.phaseshaper.useAntiAliasing;

    /* Epsilon added to the minimum value for v in order to avoid clipping on transition
       (this occurs since both vertical and horizontal domains of the inflection vector
        are symmetric around 0.5.) */
    input event
    {
        float horizontal [[ name: "Horizontal",   min: 0,       max: 1, init: initialHorizontalValue, unit: ""]];
        float vertical   [[ name: "Vertical",     min: 0.50001, max: 1, init: initialVerticalValue  , unit: ""]];
    }

    input event bool antiAliasing [[ name: "Anti-Aliasing", init: true, text: "off|on", boolean ]];

    let voiceCount = 8;
    const float initialHorizontalValue = 0.5f;
    const float initialVerticalValue   = 0.75f;

    node
    {
        voices = Voice[voiceCount];
        voiceAllocator = std::voices::VoiceAllocator (voiceCount);
    }

    node horizontalSmoothing = std::smoothing::SmoothedValueStream (0.2f, initialHorizontalValue);
    node verticalSmoothing   = std::smoothing::SmoothedValueStream (0.2f, initialVerticalValue);

    connection
    {
        // Convert the midi message to a our std::notes messages and forward to the voice allocator
        midiIn -> std::midi::MPEConverter -> voiceAllocator;

        // Plumb the voice allocator to the voices array
        voiceAllocator.voiceEventOut -> voices.eventIn;

        horizontal -> horizontalSmoothing -> voices.input1;
        vertical   -> verticalSmoothing   -> voices.input2;

        antiAliasing -> voices.useAntiAliasing;

        // Sum the voices audio out to the output
        voices -> std::levels::ConstantGain (float, 0.1f) -> out;
    }
}

graph Voice
{
    input event (std::notes::NoteOn, std::notes::NoteOff) eventIn;
    output stream float out;
    
    input stream
    {
        float input1;
        float input2;
    } 

    input event bool useAntiAliasing;

    node
    {
        noteToFrequency = NoteToFrequency;
        envelope = std::envelopes::FixedASR (0.01f, 2.5f);
        phasor = std::oscillators::Phasor (float32);
        phaseshaper = vps::Processor;
    }

    connection
    {
        eventIn -> noteToFrequency -> phasor.frequencyIn;
        eventIn -> envelope.eventIn;

        input1 -> phaseshaper.horizontal;
        input2 -> phaseshaper.vertical;

        useAntiAliasing -> phaseshaper.useAntiAliasing;

        phasor  -> phaseshaper.in;
        (envelope.gainOut * phaseshaper.out) -> out;
    }
}

// Determine the note frequency from the pitch (midi note)
processor NoteToFrequency
{
    input event std::notes::NoteOn eventIn;
    output event float32 frequencyOut;

    event eventIn (std::notes::NoteOn e)
    {
        frequencyOut <- std::notes::noteToFrequency (e.pitch);
    }
}

namespace vps 
{
    struct InflectionPoint { float d, v; }

    float twoDimensionalPhaseDistortion(const float& x, const InflectionPoint& p)
    {
        // Piecewise function expressed with simple conditional logic.
        if (x <= p.d)
            return p.v * x / p.d;
        else
            return (1.0f - p.v) * (x - p.d) / (1.0f - p.d) + p.v;
    }

    void aliasingSuppression(const float& phaseshaper, float& out, const float& v) 
    {
        float modifiedPhaseshaper = 0.0f;
        float b = fmod(v, 1.0f);
        
        if (0 < b && b <= 0.5f) {
            modifiedPhaseshaper = fmod(phaseshaper, 1.0f) / (2.0f * b);
        }
        else if (0.5f < b && b < 1.0f) {
            modifiedPhaseshaper = fmod(phaseshaper, 1.0f) / b;
        }
        
        // When applied to equation (1), the incomplete segment is rendered as a smooth full-cycle sinusoid,
        // which is then scaled and offset in relation to c = cos(2πb).
        float c = cos(float(twoPi * b));
        
        if (0 < b && b <= 0.5f) {
            out = (((1.0f - c) * out) - 1.0f - c) / 2.0f;
        }
        else if (b > 0.5f || modifiedPhaseshaper > 0.5f) {
            out = (((1.0f + c) * out) + 1.0f - c) / 2.0f;
        }

        // Does the comma in a piecewise function mean || or && ?
        // else if (b > 0.5f && modifiedPhaseshaper > 0.5f) {
        //     out = (((1.0f + c) * out) + 1.0f - c) / 2.0f;
        // }
    }

    processor Processor
    {
        input  stream float in;
        output stream float out;

        input stream 
        {
            float horizontal;
            float vertical;
        }

        input event bool useAntiAliasing;

        // Sinusoid
        var p = InflectionPoint(0.5f, 0.5f);
        bool antiAliasing = true;

        // (0, 0) also produces a sinewave - but initializing the inflection point this way causes the audio driver to crash!
        // let p = InflectionPoint(0.0f, 0.0f);

        event useAntiAliasing (bool b) { antiAliasing = b; }

        void main()
        {
            loop 
            {
                // TODO: Is there a cleaner way to handle these control changes?
                p.d = horizontal;
                p.v = vertical;
                
                float vps = twoDimensionalPhaseDistortion(in, p);    // A phaseshaper acting on an input signal.
                float signal = -cos(float(twoPi * vps));

                // TODO: Use spectrum analyzer to check anti-aliasing is 100% correct.
                if (antiAliasing) {
                    // The aliasing produced by the incomplete periods may also be suppressed
                    // by exploiting a novel single oscillator algorithm, which modifies the
                    // phaseshaper when φvps[φ(n)] > floor(v), i.e. when the phase is inside
                    // the incomplete period.
                    if (vps > floor(p.v)) {
                        aliasingSuppression(vps, signal, p.v);
                    }
                }

                out <- signal;
                advance();
            }
        }
    }
}